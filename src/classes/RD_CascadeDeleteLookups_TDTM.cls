/*
    Copyright (c) 2017, Salesforce.org
    All rights reserved.
    
    Redistribution and use in source and binary forms, with or without
    modification, are permitted provided that the following conditions are met:
    
    * Redistributions of source code must retain the above copyright
      notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright
      notice, this list of conditions and the following disclaimer in the
      documentation and/or other materials provided with the distribution.
    * Neither the name of Salesforce.org nor the names of
      its contributors may be used to endorse or promote products derived
      from this software without specific prior written permission.

    THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
    "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT 
    LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS 
    FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE 
    COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, 
    INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, 
    BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
    LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER 
    CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT 
    LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN 
    ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE 
    POSSIBILITY OF SUCH DAMAGE.
*/
/**
 * @author Salesforce.org
 * @date 2017
 * @group Utilities
 * @description Cascade deletion extension for Recurring Donation object.
 */
public with sharing class RD_CascadeDeleteLookups_TDTM extends CDL_CascadeDeleteLookups_TDTM {

    /*******************************************************************************************************
    * @description Gets the deleted Recurring Donations CascadeDeleteLoader Object. 
    * @return CDL_CascadeDeleteLookups.CascadeDeleteLoader.
    ********************************************************************************************************/
    protected override CDL_CascadeDeleteLookups.CascadeDeleteLoader getCascadeDeleteLoader() {
        return new CascadeDeleteLoader();
    }

    /*******************************************************************************************************
    * @description Gets the undeleted Recurring Donations CascadeUndeleteLoader Object.
    * @return CDL_CascadeDeleteLookups.CascadeUndeleteLoader.
    ********************************************************************************************************/
    protected override CDL_CascadeDeleteLookups.CascadeUndeleteLoader getCascadeUndeleteLoader() {
        return new CascadeUndeleteLoader();
    }

    /*******************************************************************************************************
    * @description Gets the deleted Recurring Donations ChildrenValidator Object.
    * @return CDL_CascadeDeleteLookups.CascadeDeleteValidator.
    ********************************************************************************************************/
    protected override CDL_CascadeDeleteLookups.CascadeDeleteValidator getCascadeDeleteValidator() {
        return new CascadeDeleteValidator();
    }

    /*******************************************************************************************************
    * @description Class that retrieves the children of the deleted Recurring Donations.
    ********************************************************************************************************/
    class CascadeDeleteLoader implements CDL_CascadeDeleteLookups.CascadeDeleteLoader {
        
        /*******************************************************************************************************
        * @description Retrieves the related Opportunities and Allocations of the deleted Recurring Donations.
        * @param Set<Id> The Ids of the deleted Recurring Donations.
        * @return List<SObject> Deleted Recurring Donations' Opportunities and Allocations.
        ****************************************************************************    ****************************/
        public List<SObject> load(Set<Id> recurringDonationIds) {
            List<SObject> result = new List<SObject>();

            List<Opportunity> relatedOpportunities = [
                SELECT Name, npe03__Recurring_Donation__c, IsWon, IsClosed
                FROM Opportunity
                WHERE npe03__Recurring_Donation__c IN :recurringDonationIds
            ];
            result.addAll((List<SObject>) relatedOpportunities);

            List<Allocation__c> relatedAllocations = [
                SELECT Recurring_Donation__c, Opportunity__c
                FROM Allocation__c
                WHERE Recurring_Donation__c IN :recurringDonationIds
                OR Opportunity__c IN :relatedOpportunities
            ];
            result.addAll((List<SObject>) relatedAllocations);

            return (new Map<Id, SObject>(result)).values();
        }
    }

    /*******************************************************************************************************
    * @description Class that retrieves the children of the undeleted Recurring Donations.
    ********************************************************************************************************/
    class CascadeUndeleteLoader implements CDL_CascadeDeleteLookups.CascadeUndeleteLoader {

        /*******************************************************************************************************
        * @description Retrieves the related Opportunities and Allocations of the undeleted Recurring Donations.
        * @param Set<Id> The Ids of the undeleted Recurring Donations.
        * @return List<SObject> Undeleted Recurring Donations's Opportunities and Allocations.
        ********************************************************************************************************/
        public List<SObject> load(Set<Id> recurringDonationIds) {
            List<SObject> result = new List<SObject>();

            List<Opportunity> relatedOpportunities = [
                SELECT Name
                FROM Opportunity
                WHERE npe03__Recurring_Donation__c IN :recurringDonationIds
                AND IsDeleted = TRUE
                ALL ROWS
            ];
            result.addAll((List<SObject>) relatedOpportunities);

            List<Allocation__c> relatedAllocations = [
                SELECT Id
                FROM Allocation__c
                WHERE (Recurring_Donation__c IN :recurringDonationIds OR Opportunity__c IN :relatedOpportunities)
                AND IsDeleted = TRUE
                ALL ROWS
            ];
            result.addAll((List<SObject>) relatedAllocations);

            return (new Map<Id, SObject>(result)).values();
        }

        /*******************************************************************************************************
        * @description Retrieves the next children records group to be undeleted. In this case, there is no other
        * group of children records to undelete after Opportunities and Allocations undeletion.
        * @return CDL_CascadeDeleteLookups.CascadeUndeleteLoader.
        ********************************************************************************************************/
        public CDL_CascadeDeleteLookups.CascadeUndeleteLoader next() {
            return null;
        }
    }

    /*******************************************************************************************************
    * @description Class that validates the children of the deleted Recurring Donations.
    ********************************************************************************************************/
    class CascadeDeleteValidator implements CDL_CascadeDeleteLookups.CascadeDeleteValidator {

        /*******************************************************************************************************
        * @description validates the children of the Account.
        * @param List<npe03__Recurring_Donation__c> The deleted Recurring Donations.
        * @param List<SObject> The The deleted Recurring Donations children to validate.
        * @return List<Object> The deleted Recurring Donations children that can be deleted.
        ********************************************************************************************************/
        public List<SObject> validateDeletion(List<npe03__Recurring_Donation__c> deletedRecurringDonations, List<SObject> children) {
            List<SObject> validChildren = new List<SObject>();

            for(npe03__Recurring_Donation__c deletedRecurringDonation : deletedRecurringDonations) {
                CDL_CascadeDeleteLookups.Error error = new CDL_CascadeDeleteLookups.Error(deletedRecurringDonation.Name);
                for (SObject child : children) {
                    if (isClosedWonOpportunity(child, deletedRecurringDonation.Id)) {
                        Opportunity closedWonOpportunity = (Opportunity) child;
                        error.addChildError(Label.ClosedWonOpportunities, closedWonOpportunity.Id, closedWonOpportunity.Name);
                    }
                    else {
                        validChildren.add(child);
                    }
                }

                String errorMessage = error.getMessage();
                if (String.isNotBlank(errorMessage)) {
                    deletedRecurringDonation.addError(errorMessage, false);
                }
            }

            return validChildren;
        }

        /*******************************************************************************************************
        * @description Checks if the record is a Closed Won Opportunity.
        * @param SObject The child record.
        * @param Id The Id of the deleted Recurring Donation.
        * @return Boolean.
        ********************************************************************************************************/
        private Boolean isClosedWonOpportunity(SObject record, Id deletedRecurringDonatioinId) {
            return (
                record instanceof Opportunity && 
                ((Opportunity) record).npe03__Recurring_Donation__c == deletedRecurringDonatioinId &&
                ((Opportunity) record).IsClosed && ((Opportunity) record).IsWon
            );
        }
    }
}